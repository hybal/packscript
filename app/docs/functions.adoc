= Functions
:hardbreaks:


Functions are basically everything in PackScript, since it is a functional language they are also values that can be passed around.

to call a function in PackScript you just write the function name followed by the parameters it takes.
[source, packscript]
----
print "Hello, World"
----

Note that there are no parenthesis, in PackScript parenthesis are optional and for the most part you do not need to include them.

For those coming from other functional languages (mainly Haskell), the syntax for parameters is a little bit different. Mainly that commas are required to separate the different parameters.
[source, packscript]
----
add 1, 2 #adds two numbers; Returns: 3
----

Creating a function is similar to how you create a variable, the only difference is the addition of parameters before the equal(=) sign 
[source, packscript]
----
let add (a,b) = a + b
----

Note that if a function does not take any parameters you still have to include the parenthesis. This is to distinguish between a function and a variable.
[source, packscript]
----
let num () = 123456 #this is a function, you have to call it with num() to get the value
let num2 = 123456 #this is just a variable and cannot be called (unless the value is a function as well)
----

If you want a function to do more than just evaluate a single expression, you can use a *block*. In which case the last expression in the block is what it evaluates to. 
You can also use the `return` keyword to return early from a block.
[source, packscript]
----
let print_me (str) = {
    print "I say hi"
    print "I also say "..str
    true
}
----
If you need a function to return nothing you can return *unit* (`()`).
[source, packscript]
----
let ret_nothing () = {
    print "Nothing is returned from me"
    ()
}
----
In the example above you could also just have the call to print, since print returns unit.
[source, packscript]
----
let ret_nothing () = {
    print "Nothing is returned from me"
}
----

Blocks are essentially just lambdas or anonymous functions so you can use them with normal variables as well
[source, packscript]
----
let normal_var = {
    print "I am a block"
}

normal_var() #prints: I am a block
----

if you need to pass parameters into a block you can declare them before the block (you can also do this with just an expression).
[source, packscript]
let block_with_params = (a,b) -> {
    print $"Adding {a} and {b}" 
    a+b
}

[%collapsible]
====
The reason why you do not have to specify the parameters that a block takes in a function is because blocks "capture" the surrounding variables, including the parameters of the function.
Technically you could also include specific parameters to a block in a function, but there is not many uses for that.
====

=== Currying

Functions in PackScript can also be _curried_ which means you can pass only a portion of the required parameters into a function and it will just return a _new_ function that takes the rest of the parameters. Only after all of the parameters have been passed will the function be executed.
[source, packscript]
----
let add (a,b) = a + b

let partial_add = add 35 #returns a new function that takes b

print partial_add 89 #prints 124
----

== Shell Functions

[WARNING]
You should only use shell functions when required, since shell commands are not cross-platform you cannot guarantee that they will work everywhere. Instead try to use the standard library functions as much as possible.

You can easily run any arbitrary shell function by just starting a line with a bang (!)
[source, packscript]
----
!echo "Hello, World!"
----

However, the process is not privileged by default (so on linux you can't use `sudo`). However you can ask for those permissions by adding another bang (!!). This will pause the script and ask the user for permission to elevate privileges. (this will likely change)
[source, packscript]
!!sudo apt update

[NOTE]
every bang-command uses the same process that is created on startup, if you need to create a new one see `exec`

Shell Functions are treated as the same thing as normal functions, so they can be used as values. To pass them as a value you have to prefix them with an ampersand (&).
[source, packscript]
let shell = &!echo "Hello, World!"

